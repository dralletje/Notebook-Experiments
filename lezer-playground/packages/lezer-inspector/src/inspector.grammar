@top Inspected { Node }

Name { name }
Node {
  ( Error | String | Name )
  meta?
  Arguments?
}
Error { @specialize[@name="⚠"]<name, "⚠"> }
Arguments { "{" (Node)* "}" }

meta {
  Props?
  Position
}
Props {
  "["
  Prop ("," Prop)*
  "]"
}
Prop {
  PropName { "@"? name }
  "="
  PropValue { Name | String }
}
Position {
  "<" Number "," Number ">"
}

@skip { spaces | newline | tabs }

@tokens {
  // From lezer source directly
  name { (@asciiLetter | @digit | $[\-_\u{a1}-\u{10ffff}])+ }
  Number { @digit+ }
  String { '"' (![\\\n"] | "\\" _)* '"' }
  newline { $[\r\n] }
  spaces { " "+ }
  tabs { "\t"+ }
  commas { "," }
}

@external propSource inspectorHighlight from "./highlight"
